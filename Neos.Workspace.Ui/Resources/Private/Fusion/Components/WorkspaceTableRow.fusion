prototype(Neos.Workspace.Ui:Component.WorkspaceTableRow) < prototype(Neos.Fusion:Component) {
  /// Neos\ContentRepository\Core\Projection\Workspace\Workspace
  userWorkspace = ''
  /// string
  workspaceName = ''
  /// array{ workspace, changesCounts, canPublish, canManage, canDelete, workspaceOwnerHumanReadable }
  workspaceData = ${{}}
  /// boolean
  canManage = false
  /// integer
  level = 0

  @private {
    i18n = ${I18n.id('').source('Main').package('Neos.Workspace.Ui')}
    workspace = ${props.workspaceData.workspace}
    workspaceTableRowId = ${'workspace-row-' + props.workspaceName}
    workspaceStatus = Neos.Fusion:Case {
      personal-workspace {
        condition = ${props.workspaceName == props.userWorkspace.workspaceName.value}
        renderer = 'personal-workspace'
      }
      stale {
        // TODO: Calculate stale status based on last change data
        condition = false
        renderer = 'stale'
      }
      withAcl {
        // TODO: Calculate acl status on whether the workspace is shared with selected users
        condition = false
        renderer = 'with-acl'
      }
      private {
        condition = ${props.workspaceData.workspace.workspaceOwner}
        renderer = 'private'
      }
      default {
        condition = true
        renderer = 'internal'
      }
    }
    workspaceStatusLabel = Neos.Fusion:Match {
      @subject = ${private.workspaceStatus}
      @default = ${private.i18n.id('table.column.access.internal').value('Internal workspace')}
      personal-workspace = ${private.i18n.id('badge.isPersonalWorkspace').value('This is your personal workspace')}
      stale = ${private.i18n.id('badge.isStale').value('This workspace has not been used for a long time')}
      with-acl = ${private.i18n.id('table.column.access.acl').value('This workspace is owned by ' + props.workspaceData.workspaceOwnerHumanReadable + ' but allows access to additional users')}
      private = ${private.i18n.id('table.column.access.private').value('This workspace is owned by ' + props.workspaceData.workspaceOwnerHumanReadable)}
    }
    workspaceStatusIcon = Neos.Fusion:Match {
      @subject = ${private.workspaceStatus}
      @default = 'user'
      internal = 'users'
      with-acl = 'user-plus'
    }
    showWorkspaceUri = Neos.Fusion:ActionUri {
      action = 'show'
      arguments {
        workspace = ${props.workspaceName}
      }
    }
    editWorkspaceUri = Neos.Fusion:ActionUri {
      action = 'edit'
      arguments {
        workspaceName = ${props.workspaceName}
      }
    }
    deleteWorkspaceUri = Neos.Fusion:ActionUri {
      action = 'delete'
      format = 'htmx'
      arguments {
        workspaceName = ${props.workspaceName}
      }
    }
    deleteWorkspacePopoverId = 'workspace-delete-modal'
  }

  renderer = afx`
    <tr id={private.workspaceTableRowId} class={'workspace--' + private.workspaceStatus}>
      <td class="workspace-type-column" title={private.workspaceStatusLabel}>
        <Neos.Workspace.Ui:Component.Icon
          icon={private.workspaceStatusIcon}
          secondaryIcon={private.workspaceStatus == 'stale' ? 'clock' : ''}
        />
      </td>
      <td class="workspace-label-column" style={'--indent-level:' + props.level }>
        <Neos.Workspace.Ui:Component.Icon icon="level-up-alt" @if={props.level > 1}/>
        {props.workspaceData.workspace.workspaceTitle.value}
        <span class="workspace__info-text" @if={props.workspaceName == props.userWorkspace.workspaceName.value}>
          ({private.i18n.id('workspaces.workspace.userWorkspace')})
        </span>
      </td>
      <td class="workspace-description-column">
        {props.workspaceData.workspace.workspaceDescription.value || '–'}
      </td>
      <td>{props.workspaceData.workspaceOwnerHumanReadable || '–'}</td>
      <td>–</td>
      <td>{props.workspaceData.workspace.status.value || '–'}</td>
      <td>–</td>
      <td class="workspace-action-column">
        <a class="neos-button"
           title={private.i18n.id(props.workspaceData.changesCounts.total ? 'workspaces.reviewWorkspace' : 'workspaces.reviewWorkspace.noChanges').arguments([private.workspace.workspaceTitle.value])}
           disabled={props.workspaceData.changesCounts.total == 0}
           href={private.showWorkspaceUri}
        >
          <Neos.Workspace.Ui:Component.Icon icon="review"/>{' '}
          {private.i18n.id('workspaces.reviewWorkspace.label')}
        </a>
        <a
          class="neos-button"
          title={private.i18n.id('workspaces.editWorkspace').arguments([private.workspace.workspaceTitle.value])}
          href={private.editWorkspaceUri}
          @if={!props.canManage || props.workspaceData.changesCounts == null}
        >
          <Neos.Workspace.Ui:Component.Icon icon="pencil-alt"/>
        </a>
        <button
          class="neos-button neos-button-danger"
          title={private.i18n.id('workspaces.deleteWorkspace').arguments([private.workspace.workspaceTitle.value])}
          disabled={props.workspaceData.changesCounts.total > 0}
          hx-get={private.deleteWorkspaceUri}
          hx-target='#popover-container'
          hx-swap='innerHTML'
          hx-on--after-request={'document.getElementById("' + private.deleteWorkspacePopoverId + '").showPopover()'}
        >
          <Neos.Workspace.Ui:Component.Icon icon="trash-alt icon-white"/>
        </button>
      </td>
    </tr>
  `
}
