##
# Renders a single workspace list item and its subworkspaces
#
prototype(Neos.Workspace.Ui:Component.WorkspaceTreeNode) < prototype(Neos.Fusion:Component) {
  /// Neos\ContentRepository\Core\Projection\Workspace\Workspace
  userWorkspace = null
  /// Neos\Workspace\Ui\Model\WorkspaceDetails
  workspaceDetails = null
  /// Neos\Workspace\Ui\Model\WorkspaceDetailsCollection
  workspacesAndChangeCounts = ${{}}
  /// int
  level = 0

  @private {
    workspaceName = ${props.workspaceDetails ? props.workspaceDetails.workspace.workspaceName.value : 'live'}
    subWorkspacesAndChangeCounts = ${Array.filter(props.workspacesAndChangeCounts, (workspaceData) => workspaceData.workspace.baseWorkspaceName.value == private.workspaceName)}
  }

  renderer = afx`
    <Neos.Workspace.Ui:Component.WorkspaceTableRow
      userWorkspace={props.userWorkspace}
      workspaceDetails={props.workspaceDetails}
      level={props.level}
      @if={props.workspaceName != 'live'}
    />
    <Neos.Fusion:Loop items={private.subWorkspacesAndChangeCounts} itemName="workspaceDetails">
      <Neos.Workspace.Ui:Component.WorkspaceTreeNode
        userWorkspace={props.userWorkspace}
        workspaceDetails={workspaceDetails}
        workspacesAndChangeCounts={props.workspacesAndChangeCounts}
        level={props.level + 1}
      />
    </Neos.Fusion:Loop>
  `
}
