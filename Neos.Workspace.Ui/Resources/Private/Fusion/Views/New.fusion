Neos.Workspace.Ui.WorkspaceController.new = Neos.Fusion:Component {
  flashMessages = ${flashMessages}
  baseWorkspaceOptions = ${baseWorkspaceOptions}
  workspace = ${workspace}

  prototype(Neos.Fusion.Form:LabelRenderer) {
    translationPackage = 'Neos.Workspace.Ui'
    translationSource = 'Main'
  }

  renderer = afx`
    <Neos.Workspace.Ui:Component.FlashMessages flashMessages={props.flashMessages}/>
    <main>
      <h2>{I18n.translate('workspaces.createNewWorkspace', 'Create new workspace', [], 'Main', 'Neos.Workspace.Ui')}</h2>
      <Neos.Fusion.Form:Form form.data.workspace={props.workspace} form.target.action="create">
        <fieldset>
          <Neos.Fusion.Form:FieldContainer
            field.name="title"
            label="workspaces.workspace.title"
            class="neos-control-group"
          >
            <Neos.Fusion.Form:Textfield/>
          </Neos.Fusion.Form:FieldContainer>

          <Neos.Fusion.Form:FieldContainer
            field.name="description"
            label="workspaces.workspace.description"
            class="neos-control-group"
          >
            <Neos.Fusion.Form:Textarea attributes.rows={3} />
          </Neos.Fusion.Form:FieldContainer>

          <Neos.Fusion.Form:FieldContainer
            field.name="baseWorkspace"
            field.value="live"
            label="workspaces.workspace.baseWorkspace"
            class="neos-control-group"
          >
            <Neos.Fusion.Form:Select>
              <Neos.Fusion:Loop items={props.baseWorkspaceOptions} itemName="workspaceTitle" itemKey="workspaceName">
                <Neos.Fusion.Form:Select.Option
                  option.value={workspaceName}>{workspaceTitle}</Neos.Fusion.Form:Select.Option>
              </Neos.Fusion:Loop>
            </Neos.Fusion.Form:Select>
          </Neos.Fusion.Form:FieldContainer>
        </fieldset>
        <fieldset>
          <Neos.Fusion:Fragment
            @if={Security.hasAccess('Neos.Workspace.Ui:Backend.Module.Management.Workspace.ManageInternalWorkspaces')}>
            <h3 class="neos-control-label">
              {I18n.translate('workspaces.workspace.visibility', 'Visibility', [], 'Main', 'Neos.Workspace.Ui')}
            </h3>
            <Neos.Fusion.Form:FieldContainer
              field.name="visibility"
              class="neos-control-group"
            >
              <Neos.Fusion.Form:Radio id="visibility.private" field.value="private" attributes.checked={true}>
                <span></span>
                <span>
                  {I18n.translate('workspaces.workspace.visibility.private', 'Private', [], 'Main', 'Neos.Workspace.Ui')}
                  <span class="neos-help-inline">
                    {I18n.translate('workspaces.workspace.visibility.private.help', 'Only reviewers and administrators can access and modify this workspace', [], 'Main', 'Neos.Workspace.Ui')}
                  </span>
                </span>
              </Neos.Fusion.Form:Radio>
              <br/>
              <Neos.Fusion.Form:Radio id="visibility.internal" field.value="internal">
                <span></span>
                <span>
                  {I18n.translate('workspaces.workspace.visibility.internal', 'Internal', [], 'Main', 'Neos.Workspace.Ui')}
                  <span class="neos-help-inline">
                    {I18n.translate('workspaces.workspace.visibility.internal.help', 'Any logged in editor can see and modify this workspace.', [], 'Main', 'Neos.Workspace.Ui')}
                  </span>
                </span>
              </Neos.Fusion.Form:Radio>
            </Neos.Fusion.Form:FieldContainer>
          </Neos.Fusion:Fragment>

          <Neos.Fusion.Form:Hidden
            name="visibility"
            value="private"
            @if={!Security.hasAccess('Neos.Workspace.Ui:Backend.Module.Management.Workspace.ManageInternalWorkspaces')}
          />

          <Neos.Fusion.Form:Button attributes.class="neos-button neos-button-primary">
            {I18n.translate('workspaces.createWorkspace', 'Create workspace', [], 'Main', 'Neos.Workspace.Ui')}
          </Neos.Fusion.Form:Button>
        </fieldset>
      </Neos.Fusion.Form:Form>

      <Neos.Workspace.Ui:Component.Footer>
        <Neos.Fusion:Link.Action href.action="index" class="neos-button">
          {I18n.translate('cancel', 'Cancel', [], 'Main', 'Neos.Workspace.Ui')}
        </Neos.Fusion:Link.Action>
      </Neos.Workspace.Ui:Component.Footer>
    </main>
  `
}
